//빌드시 가장 먼저 시작 하는 스크립트, 최상단에 정의 (플로그인 설정에 영향을 미침)
buildscript {
    ext {
        springBootVersion = '2.6.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

// 하위 모든 프로젝트 공통 세팅
//plugins {
//    id 'java'
//    id 'application'
//    id 'org.springframework.boot' version '2.4.5.RELEASE'
//} 으로도 정의 가능 version 으로 버전도 정의 가능.
subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library' //없으면 의존성 주입시 api project 사용불가
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.example'
    version '1.0-SNAPSHOT'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}

// 개별 설정 , 의존성 관리
project(':test-core') {
    bootJar { enabled = false } // core 은 bootJar 로 패키징 할 필요 없음
    jar { enabled = true }

    dependencies {
    }
}

project(':test-api-server') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        api project(':test-core') // 컴파일 시 velog-core project 로딩
//        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':test-batch-server') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        api project(':test-core') // 컴파일 시 velog-core project 로딩
    }
}